#!/usr/bin/env maiascript

///
/// @license
/// Copyright 2020 Roberto Luiz Souza Monteiro,
///                Renata Souza Barreto,
///                Hernane Borges de Barros Pereira.
///
/// Licensed under the Apache License, Version 2.0 (the "License")
/// you may not use this file except in compliance with the License.
/// You may obtain a copy of the License at
///
///   http://www.apache.org/licenses/LICENSE-2.0
///
/// Unless required by applicable law or agreed to in writing, software
/// distributed under the License is distributed on an "AS IS" BASIS,
/// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, eitherMath.express or implied.
/// See the License for the specific language governing permissions and
/// limitations under the License.
///

///
/// MaiaApp core class.
/// @namespace maiastatistics
///
namespace maiastatistics {
    appTitle = "MaiaStatistics"
    appName = "maiastatistics"
    scriptPath = "./"
    dfaLibrary = this.scriptPath + "/maia/dfa/dfa.maia"
    
    ///
    /// Creates a new time series containing only the specified column.
    /// @method getNextSteps
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @param {number}   columnNumber - columnNumber.
    /// @return {object}  Returns a new time series containing only the specified column.
    ///
    function getColumn(data, columnNumber) {
        newSeriesData = []
        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, columnNumber]
            newSeriesData.push([x])
        }
        return(newSeriesData)    }

    ///
    /// Replaces a given pattern in a time series.
    /// @method replacePattern
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @param {string}   pattern - The search pattern.
    /// @param {string}   replacementString - The replacement value for the given pattern.
    /// @param {boolean}  asNumber - Converts the series data to number.
    /// @return {object}  Returns the replaced time series.
    ///
    function replacePattern(data, pattern, replacementString, asNumber) {
        if (core.type(asNumber) == "undefined") {
            asNumber = false
        }
        replacedData = []
        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, 0]
            if (asNumber) {
                if (x == pattern) {
                    replacedData.push([core.toNumber(replacementString)])
                } else {
                    replacedData.push([core.toNumber(x)])
                }
            } else {
                if (x == pattern) {
                    replacedData.push([replacementString])
                } else {
                    replacedData.push([x])
                }
            }
        }
        return(replacedData)
    }

    ///
    /// Split an array into two parts.
    /// @method splitData
    /// @memberof maiastatistics
    /// @param {object}   data - The data to be split.
    /// @param {number}   nFirst - Number of rows of the first part of the array.
    /// @return {object}  Returns two arrays.
    ///
    function splitData(data, nFirst) {
        part1 = []
        part2 = []
        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, 0]
            if (i < nFirst) {
                part1.push([core.toNumber(x)])
            } else {
                part2.push([core.toNumber(x)])
            }
        }
        returnObject = {
            "part1": part1,
            "part2": part2
        }
        return(returnObject)
    }

    ///
    /// Calculates the average and the deviation of a time series.
    /// @method getAverage
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @return {object}  Returns the average and the deviation of the time series.
    ///
    function getAverage(data) {
        average = 0
        deviation = 0
        minimum = data[0, 0]
        maximum = data[0, 0]
        sx = 0
        sx2 = 0
        n = data.length
        for (i = 0; i < data.length; i = i + 1) {
            if (core.type(data[i]) == "undefined") {
                continue
            }
            if (core.type(data[i]) == "number") {
                x = data[i]
            } else {
                x = data[i, 0]
            }
            if (core.type(x) == "number") {
                if (core.type(minimum) == "undefined") {
                    minimum = x
                }
                if (core.type(maximum) == "undefined") {
                    maximum = x
                }
                if (minimum > x) {
                    minimum = x
                }
                if (maximum < x) {
                    maximum = x
                }
                sx = sx + x
                sx2 = sx2 + x * x
            }
        }
        average = sx / n
        deviation = math.sqrt((sx2 - (sx * sx) / n) / (n - 1))
        error = deviation / math.sqrt(n)
        returnObject = {
            "average": average,
            "deviation": deviation,
            "error": error,
            "minimum": minimum,
            "maximum": maximum,
            "n": n
        }
        return(returnObject)
    }

    ///
    /// Normalizes a time series.
    /// @method normalize
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @param {number}   minimum - The minimum value of the time series.
    /// @param {number}   maximum - The maximum value of the time series.
    /// @return {object}  Returns the normalized time series.
    ///
    function normalize(data, minimum, maximum) {
        if ((core.type(minimum) == "undefined") || (core.type(maximum) == "undefined")) {
            avgData = this.getAverage(data)
            minimum = avgData.minimum
            maximum = avgData.maximum
        }
        normalizedData = []
        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, 0]
            if (core.type(x) == "number") {
                normalizedX = (x - minimum) / (maximum - minimum)
                normalizedData.push([normalizedX])
            }
        }
        return(normalizedData)
    }

    ///
    /// Denormalizes a time series.
    /// @method denormalize
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @param {number}   minimum - The minimum value of the time series.
    /// @param {number}   maximum - The maximum value of the time series.
    /// @return {object}  Returns the denormalized time series.
    ///
    function denormalize(data, minimum, maximum) {
        denormalizedData = []
        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, 0]
            if (core.type(x) == "number") {
                denormalizedX = x * maximum - x * minimum + minimum
                denormalizedData.push([denormalizedX])
            }
        }
        return(denormalizedData)
    }

    ///
    /// Creates a new series containing the next steps for each value of the given series.
    /// @method getNextSteps
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @param {number}   steps - Number of next steps.
    /// @return {object}  Returns a new series containing the next steps for each value of the given series.
    ///
    function getNextSteps(data, steps) {
        nextStepData = []
        for (i = 0; i < data.length - steps; i = i + 1) {
            row = []
            for (j = i; j < i + steps; j = j + 1) {
                x = data[j, 0]
                if (core.type(x) == "number") {
                    row.push(x)
                }
            }
            if (row.length > 0) {
                nextStepData.push(row)
            }
        }
        return(nextStepData)
    }

    ///
    /// Calculates the Pearson coefficient.
    /// @method getPearson
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @return {object}  Returns the Pearson coefficient.
    ///
    function getPearson(data) {
        r = 0
        dimData = core.dim(data)
        n = dimData[0]
        avgDataX = matrix.avg(data, 0, 0, n - 1, 0)
        avgDataY = matrix.avg(data, 0, 1, n - 1, 1)
        avgX = avgDataX.avg
        devX = avgDataX.dev
        avgY = avgDataY.avg
        devY = avgDataY.dev
        sumXY = 0
        for (i = 0; i < n; i = i + 1) {
            sumXY = sumXY + data[i, 0] * data[i, 1]
        }
        r = (sumXY - n * avgX * avgY) / ((n - 1) * devX * devY)
        return(r)
    }

    ///
    /// Obtains the best curve that approximates a set of points, using the least squares method.
    /// References:
    /// Weisstein, Eric W. "Least Squares Fitting".
    /// From MathWorld--A Wolfram Web Resource.
    /// http://mathworld.wolfram.com/LeastSquaresFitting.html 
    /// @method getLsqr
    /// @memberof maiastatistics
    /// @param {object}   data - The time series.
    /// @param {number}   begin - The the first point of time series.
    /// @param {number}   end - The the last point of time series.
    /// @return {object}  Returns the best curve that approximates a set of points.
    ///
    function getLsqr(data, begin, end) {
        dimData = core.dim(data)
        dimI = dimData[0]
        dimJ = dimData[1]
        
        if (core.type(begin) == "undefined") {
            begin = 0
        }
        if (core.type(end) == "undefined") {
            end = dimI - 1
        }

        x = 0.0
        y = 0.0
        sx = 0.0
        sy = 0.0
        sxy = 0.0
        sx2 = 0.0
        sy2 = 0.0
        n = 0

        // Read each value of the series and calculates
        // a and b to f(a, b) = a + b * x.
        for (i = begin; i <= end; i = i + 1) {
            if (dimJ == 2) {
                x = data[i, 0]
                y = data[i, 1]
            } else {
                x = i
                y = data[i, 0]
            }
            if (core.type(x) == "undefined") {
                continue
            }
            if (core.type(y) == "undefined") {
                continue
            }
            sx = sx + x
            sy = sy + y
            sxy = sxy + x * y
            sx2 = sx2 + x * x
            sy2 = sy2 + y * y
            n = n + 1
        }
        
        if (n == 0) {
            return([0; 0; 0])
        }

        m1 = [n, sx; sx, sx2]
        m2 = [sy; sxy]

        //ab = m1^-1 * m2
        ab = m2 / m1
        r2 = ((sxy - sx * (sy / n)) ^ 2) / ((sx2 - (sx * sx) / n) * (sy2 - (sy * sy) / n))

        returnObject = {
            "a": ab[0, 0],
            "b": ab[1, 0],
            "r2": r2
        }
        return(returnObject)
    }

    ///
    /// Performs the detrended fluctuation analysis.
    /// @method getDfa
    /// @memberof maiastatistics
    /// @param {object}   seq - Input data array.
    /// @param {number}   nfit - Order of detrending (2: linear, 3: quadratic, etc).
    /// @param {object}   rs - Array of box sizes (uniformly distributed on log scale).
    /// @param {number}   sw - Mode (0: non-overlapping windows, 1: sliding window).
    /// @return {object}  Returns the mean squared fluctuations in mse.
    ///
    function getDfa(seq, nfit, rs, sw) {
        if (core.type(nfit) == "undefined") {
            nfit = 2
        }
        if (core.type(sw) == "undefined") {
            sw = 0
        }
        
        // Calculates the DFA.
        mse = dfa.getDfa(seq, nfit, rs, sw)

        n = mse.length
        dfaData = []
        dfaLog10Data = []
        dfaData.push(["scale", "dfa", "log10(scale)", "log10(dfa)"])
        for (i = 1; i < n; i = i + 1) {
            x = math.log10(rs[i])
            y = math.log10(mse[i]) / 2.0
            dfaData.push([rs[i], math.sqrt(mse[i]), x, y])
            dfaLog10Data.push([x, y])
        }

        // Calculates the least squares fit.
        lsqr = this.getLsqr(dfaLog10Data)

        returnObject = {
            "dfa": dfaData,
            "lsqr": lsqr
        }
        return(returnObject)
    }

    ///
    /// Performs the detrended cross-Correlation analysis.
    /// @method getDcca
    /// @memberof maiastatistics
    /// @param {object}   seq1 - Input data array 1.
    /// @param {object}   seq2 - Input data array 2.
    /// @param {number}   nfit - Order of detrending (2: linear, 3: quadratic, etc).
    /// @param {object}   rs - Array of box sizes (uniformly distributed on log scale).
    /// @return {object}  Returns the detrended cross-Correlation analysis values.
    ///
    function getDcca(seq1, seq2, nfit, rs) {
        if (core.type(nfit) == "undefined") {
            nfit = 2
        }

        // Calculates the DFA.
        dcca = dfa.getDcca(seq1, seq2, nfit, rs)

        n = dcca.length
        dccaData = []
        dccaData.push(["scale", "dcca"])
        for (i = 1; i < n; i = i + 1) {
            dccaData.push([rs[i], dcca[i]])
        }
        return(dccaData)
    }

    ///
    /// Performs the detrended cross-Correlation analysis.
    /// @method getRhoDcca
    /// @memberof maiastatistics
    /// @param {object}   dfa1 - DFA array of first series.
    /// @param {object}   dfa2 - DFA array of second series.
    /// @param {object}   dcca - DCCA array for the two series.
    /// @return {object}  Returns the detrended cross-Correlation analysis values.
    ///
    function getRhoDcca(dfa1, dfa2, dcca) {
        // Calculates the rhoDCCA.
        rhodcca = dfa.getRhoDcca(dfa1, dfa2, dcca)

        n = rhodcca.length
        rhoDccaData = []
        rhoDccaData.push(["scale", "dfa1", "dfa2", "dcca", "rhodcca"])
        for (i = 1; i < n; i = i + 1) {
            rhoDccaData.push([rs[i], math.sqrt(dfa1[i]), math.sqrt(dfa2[i]), dcca[i], rhodcca[i]])
        }
        return(rhoDccaData)
    }

    /// Starts the application and processes the data passed as command line arguments.
    /// @method run
    /// @memberof maiastatistics
    function run() {
        // Supports only the Node.js interpreter.
        if (core.type(process) != "undefined") {
            command = "node"            
            fs = system.require("fs")
            realPath = fs.realpathSync(system.argv[0])
            filePath = realPath.split("/")
            filePath = core.slice(filePath, 0, filePath.length - 2)
            filePath = filePath.join("/")
            this.scriptPath = filePath
            this.dfaLibrary = this.scriptPath + "/maia/dfa/dfa.maia"
            system.source(this.dfaLibrary)            
            readTextFile = fs.readFileSync
            // Read file callback.
            function readFile(input) {
                content = readTextFile(input, "utf-8")
                return(content)
            }            
            // Command line options.
            inputFile = ""
            outputFile = ""
            reportFile = ""
            csvFile = ""
            linesToSkip = 0
            separator = ","            
            prefix = "sample"
            nFiles = 1
            indexFrom = 1
            indexTo = 1
            indexIn = 1
            separateColumn = false
            columnNumber = 0
            replaceGivenPattern = false
            patternString = ""
            replacementString = ""
            splitFile = false
            nFirst = 1
            normalizeData = false
            denormalizeData = false
            minimum = ""
            maximum = ""
            nextStep = false
            steps = 1
            calculatePearson = false
            doLSQR = false
            calculateDFA = false
            calculateDCCA = false
            calculateRhoDCCA = false
            minbox = 0
            maxbox = 0
            rsmode = "peng"
            mavg = false
            nfit = 2
            sw = 0
            inputFile1 = ""
            inputFile2 = ""
            // Get command line arguments.
            if (system.argc > 1) {
                i = 1
                while (i < system.argc) {
                    if ((system.argv[i] == "-h") | (system.argv[i] == "--help")) {
                        system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
                        system.log("Options:")
                        system.log("-h     --help               Displays this help message.")
                        system.log("-i     [input file]         Input file name.")
                        system.log("-o     [output file]        Output file name.")
                        system.log("-r                          Statistics report file.")
                        system.log("-c                          Statistics CSV file.")
                        system.log("       --cf                 Correctness CSV file.")
                        system.log("       --skipfirst          Skip first line of input and output training set files.")
                        system.log("       --separator          Training set files column separator(default=,).")
                        system.log("       --prefix             Prefix for multiple neural network files (default=sample).")
                        system.log("       --nfiles             Number of files for multiple neural network files.")
                        system.log("       --from               First file index for multiple neural network files.")
                        system.log("       --to                 Last file index for multiple neural network files.")
                        system.log("       --in                 Total number of files in the file set.")
                        system.log("       --column             Creates a new time series containing only the specified column.")
                        system.log("       --replace            Replace lines whit pattern in a time series with a given value.")
                        system.log("       --with               Replacement value for the given pattern.")
                        system.log("       --split              Split a file into two parts.")
                        system.log("       --nfirst             Number of lines of the first file to split.")
                        system.log("       --normalize          Normalizes a time series.")
                        system.log("       --denormalize        Denormalizes a time series.")
                        system.log("       --minimum            The minimum value of the time series.")
                        system.log("       --maximum            The maximum value of the time series.")
                        system.log("       --nextstep           Creates a new series containing the next steps for each value of the given series.")
                        system.log("       --steps              Number of next steps.")
                        system.log("       --pearson            Calculates the Pearson coefficient.")
                        system.log("       --lsqr               Calculates the best curve that approximates a set of points, using the least squares method.")
                        system.log("       --dfa                Calculates the DFA.")
                        system.log("       --minbox             The minimum box size for DFA.")
                        system.log("       --maxbox             The maximum box size for DFA.")
                        system.log("       --rsmode             The algorithm for scaling for DFA.")
                        system.log("       --mavg               Remove the average from the series for DFA.")
                        system.log("       --nfit               The order of the regression fit for DFA.")
                        system.log("       --sw                 The sliding window mode for DFA.")
                        system.log("       --dcca               Calculates the DCCA.")
                        system.log("       --rhodcca            Calculates the rhoDCCA.")
                        system.log("       --i1                 Input file name 1 for DCCA and rhoDCCA.")
                        system.log("       --i2                 Input file name 2 for DCCA and rhoDCCA.")
                        process.exit(0)
                    } elseif (system.argv[i] == "-i") {
                        i = i + 1
                        inputFile = system.argv[i]
                    } elseif (system.argv[i] == "-o") {
                        i = i + 1
                        outputFile = system.argv[i]
                    } elseif (system.argv[i] == "-r") {
                        i = i + 1
                        reportFile = system.argv[i]
                    } elseif (system.argv[i] == "-c") {
                        i = i + 1
                        csvFile = system.argv[i]
                    } elseif (system.argv[i] == "--skipfirst") {
                        linesToSkip = 1
                    } elseif (system.argv[i] == "--separator") {
                        i = i + 1
                        separator = system.argv[i]
                    } elseif (system.argv[i] == "--avgdeg") {
                        i = i + 1
                        avgdeg = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--prefix") {
                        i = i + 1
                        prefix = system.argv[i]
                    } elseif (system.argv[i] == "--nfiles") {
                        i = i + 1
                        nFiles = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--from") {
                        i = i + 1
                        indexFrom = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--to") {
                        i = i + 1
                        indexTo = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--in") {
                        i = i + 1
                        indexIn = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--column") {
                        i = i + 1
                        columnNumber = system.argv[i]
                        separateColumn = true
                    } elseif (system.argv[i] == "--replace") {
                        i = i + 1
                        patternString = system.argv[i]
                        replaceGivenPattern = true
                    } elseif (system.argv[i] == "--with") {
                        i = i + 1
                        replacementString = system.argv[i]
                    } elseif (system.argv[i] == "--split") {
                        splitFile = true
                    } elseif (system.argv[i] == "--nfirst") {
                        i = i + 1
                        nFirst = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--normalize") {
                        normalizeData = true
                    } elseif (system.argv[i] == "--denormalize") {
                        denormalizeData = true
                    } elseif (system.argv[i] == "--minimum") {
                        i = i + 1
                        minimum = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--maximum") {
                        i = i + 1
                        maximum = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--nextstep") {
                        nextStep = true
                    } elseif (system.argv[i] == "--steps") {
                        i = i + 1
                        steps = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--pearson") {
                        calculatePearson = true
                    } elseif (system.argv[i] == "--lsqr") {
                        doLSQR = true
                    } elseif (system.argv[i] == "--dfa") {
                        calculateDFA = true
                    } elseif (system.argv[i] == "--minbox") {
                        i = i + 1
                        minbox = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--maxbox") {
                        i = i + 1
                        maxbox = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--rsmode") {
                        i = i + 1
                        rsmode = system.argv[i]
                    } elseif (system.argv[i] == "--mavg") {
                        mavg = true
                    } elseif (system.argv[i] == "--nfit") {
                        i = i + 1
                        nfit = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--sw") {
                        sw = 1
                    } elseif (system.argv[i] == "--dcca") {
                        calculateDCCA = true
                    } elseif (system.argv[i] == "--rhodcca") {
                        calculateRhoDCCA = true
                    } elseif (system.argv[i] == "--i1") {
                        i = i + 1
                        inputFile1 = system.argv[i]
                    } elseif (system.argv[i] == "--i2") {
                        i = i + 1
                        inputFile2 = system.argv[i]
                    } else {
                        if (inputFile == "") {
                            inputFile = system.argv[i]
                        } else {
                            inputFile1 = inputFile
                            inputFile2 = system.argv[i]
                        }
                    }
                    i = i + 1
                }
                if (separateColumn) {
                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()
                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()
                    if (outputFile == "") {
                        outputFile = fileName + "-column-" + core.toString(columnNumber) + "." + fileExtension
                    }
                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, true)
                    outputData = this.getColumn(inputData, columnNumber)
                    fileContents = system.createCSV(outputData, separator)
                    fs.writeFileSync(outputFile, fileContents)
                } elseif (replaceGivenPattern) {
                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()
                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()
                    if (outputFile == "") {
                        outputFile = fileName + "-replaced." + fileExtension
                    }

                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, false)
                    outputData = this.replacePattern(inputData, patternString, replacementString, true)

                    fileContents = system.createCSV(outputData, separator)
                    fs.writeFileSync(outputFile, fileContents)
                } elseif (splitFile) {
                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()
                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()

                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, false)
                    outputData = this.splitData(inputData, nFirst)

                    outputFile1 = fileName + "-" + core.toString(outputData.part1.length) + "." + fileExtension
                    outputFile2 = fileName + "-" + core.toString(outputData.part2.length) + "." + fileExtension
                    fileContents1 = system.createCSV(outputData.part1, separator)
                    fileContents2 = system.createCSV(outputData.part2, separator)
                    fs.writeFileSync(outputFile1, fileContents1)
                    fs.writeFileSync(outputFile2, fileContents2)
                } elseif (normalizeData) {
                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()
                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()
                    if (outputFile == "") {
                        outputFile = fileName + "-normalized." + fileExtension
                    }

                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, true)
                    avgData = this.getAverage(inputData)
                    outputData = this.normalize(inputData, avgData.minimum, avgData.maximum)

                    fileContents = system.createCSV(outputData, separator)
                    fs.writeFileSync(outputFile, fileContents)

                    // Saves the report file.
                    reportFileContents = ""
                    reportFileContents = reportFileContents + "Average: " + core.toString(avgData.average) + "\n"
                    reportFileContents = reportFileContents + "Standard deviation: " + core.toString(avgData.deviation) + "\n"
                    reportFileContents = reportFileContents + "Standard error: " + core.toString(avgData.error) + "\n"
                    reportFileContents = reportFileContents + "Minimum value: "  + core.toString(avgData.minimum) + "\n"
                    reportFileContents = reportFileContents + "Maximum value: "  + core.toString(avgData.maximum) + "\n"
                    reportFileContents = reportFileContents + "Observations: "  + core.toString(avgData.n) + "\n"
                    if (reportFile == "") {
                        reportFile = fileName + "-report.txt"
                    }
                    fs.writeFileSync(reportFile, reportFileContents)
                } elseif (denormalizeData) {
                    if ((core.type(minimum) != "number") || (core.type(maximum) != "number")) {
                        system.log("To denormalize a time series you need to provide the minimum and maximum values of the original, denormalized series.")
                        process.exit()
                    }
                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()
                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()
                    if (outputFile == "") {
                        outputFile = fileName + "-denormalized." + fileExtension
                    }

                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, true)
                    outputData = this.denormalize(inputData, minimum, maximum)

                    fileContents = system.createCSV(outputData, separator)
                    fs.writeFileSync(outputFile, fileContents)
                } elseif (nextStep) {
                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()
                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()
                    if (outputFile == "") {
                        outputFile = fileName + "-steps." + fileExtension
                    }

                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, true)
                    outputData = this.getNextSteps(inputData, steps)

                    fileContents = system.createCSV(outputData, separator)
                    fs.writeFileSync(outputFile, fileContents)
                } elseif (calculatePearson) {
                    glob = system.require("glob")

                    files = glob.sync(inputFile)

                    if (files.length == 0) {
                        system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
                    } else {
                        system.log("Processing " + files.length + " files...")

                        for (fileNumber = 0; fileNumber < files.length; fileNumber = fileNumber + 1) {
                            // Get the full file name.
                            file = files[fileNumber]

                            system.log("Processing file '" + file + "'...")

                            // Get the file without extension.
                            fileName = file.split(".")
                            fileName = fileName.shift()
                            // Get the file name extension.
                            fileExtension = file.split(".")
                            fileExtension = fileExtension.pop()

                            inputData = system.loadCSV(file, linesToSkip, separator, true, true)
                            pearsonCoefficient = this.getPearson(inputData)

                            // Saves the report file.
                            reportFileContents = ""
                            reportFileContents = reportFileContents + "Pearson coefficient: " + pearsonCoefficient + "\n"
                            if (reportFile == "") {
                                reportFile = fileName + "-report-pearson.txt"
                            }
                            fs.writeFileSync(reportFile, reportFileContents)

                            reportFile = ""
                        }
                    }
                } elseif (doLSQR) {
                    glob = system.require("glob")

                    files = glob.sync(inputFile)

                    if (files.length == 0) {
                        system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
                    } else {
                        system.log("Processing " + files.length + " files...")

                        for (fileNumber = 0; fileNumber < files.length; fileNumber = fileNumber + 1) {
                            // Get the full file name.
                            file = files[fileNumber]

                            system.log("Processing file '" + file + "'...")

                            // Get the file without extension.
                            fileName = file.split(".")
                            fileName = fileName.shift()
                            // Get the file name extension.
                            fileExtension = file.split(".")
                            fileExtension = fileExtension.pop()

                            inputData = system.loadCSV(file, linesToSkip, separator, true, true)

                            // Calculates the least squares fit.
                            lsqr = this.getLsqr(inputData)

                            // Saves the report file.
                            reportFileContents = ""
                            reportFileContents = reportFileContents + "y = a + b * x\n"
                            reportFileContents = reportFileContents + "a: " + lsqr.a + "\n"
                            reportFileContents = reportFileContents + "b: " + lsqr.b + "\n"
                            reportFileContents = reportFileContents + "r2: " + lsqr.r2 + "\n"
                            if (reportFile == "") {
                                reportFile = fileName + "-report-lsqr.txt"
                            }
                            fs.writeFileSync(reportFile, reportFileContents)
                            
                            reportFile = ""
                        }
                    }
                } elseif (calculateDFA) {
                    glob = system.require("glob")

                    files = glob.sync(inputFile)

                    if (files.length == 0) {
                        system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
                    } else {
                        system.log("Processing " + files.length + " files...")

                        for (fileNumber = 0; fileNumber < files.length; fileNumber = fileNumber + 1) {
                            // Get the full file name.
                            file = files[fileNumber]

                            system.log("Processing file '" + file + "'...")

                            // Get the file without extension.
                            fileName = file.split(".")
                            fileName = fileName.shift()
                            // Get the file name extension.
                            fileExtension = file.split(".")
                            fileExtension = fileExtension.pop()
                            
                            if (outputFile == "") {
                                outputFile = fileName + "-dfa." + fileExtension
                            }

                            inputData = system.loadCSV(file, linesToSkip, separator, true, true)

                            // Define the boxes sizes.
                            npts = inputData.length
                            if (minbox < 2 * nfit) {
                                minbox = 2 * nfit
                            }
                            if (maxbox == 0 || maxbox > math.round(npts / 4)) {
                                maxbox = math.round(npts / 4)
                            }
                            if (minbox > maxbox) {
                                temp = minbox
                                minbox = maxbox
                                maxbox = temp
                                if (minbox < 2 * nfit) {
                                    minbox = 2 * nfit
                                }
                            }

                            // Create the scale matrix.
                            rs = dfa.getRScale(minbox, maxbox, math.pow(2.0, 1.0/8.0), rsmode)

                            // Calculates the cumulative sum.
                            cumulativeSum = dfa.getCumulativeSum(inputData, mavg)

                            // Calculates the DFA.
                            dfaObject = this.getDfa(cumulativeSum, nfit, rs, sw)
                            dfaData = dfaObject.dfa
                            lsqr = dfaObject.lsqr

                            // Saves the DFA data.
                            fileContents = system.createCSV(dfaData, separator)
                            fs.writeFileSync(outputFile, fileContents)

                            // Saves the report file.
                            reportFileContents = ""
                            reportFileContents = reportFileContents + "y = a + b * x\n"
                            reportFileContents = reportFileContents + "a: " + lsqr.a + "\n"
                            reportFileContents = reportFileContents + "b: " + lsqr.b + "\n"
                            reportFileContents = reportFileContents + "r2: " + lsqr.r2 + "\n"
                            if (reportFile == "") {
                                reportFile = fileName + "-report-lsqr.txt"
                            }
                            fs.writeFileSync(reportFile, reportFileContents)

                            outputFile = ""
                            reportFile = ""
                        }
                    }
                } elseif (calculateDCCA) {
                    glob = system.require("glob")

                    // Get the file 1 without extension.
                    fileName1 = inputFile1.split(".")
                    fileName1 = fileName1.shift()
                    // Get the file name extension.
                    fileExtension1 = inputFile1.split(".")
                    fileExtension1 = fileExtension1.pop()
                    
                    files = glob.sync(inputFile2)
                    
                    if (files.length == 0) {
                        system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
                    } else {
                        system.log("Processing " + files.length + " files...")

                        for (fileNumber = 0; fileNumber < files.length; fileNumber = fileNumber + 1) {
                            // Get the full file name.
                            file = files[fileNumber]
                            
                            // Get the file without extension.
                            fileName2 = file.split(".")
                            fileName2 = fileName2.shift()
                            // Get the file name extension.
                            fileExtension2 = file.split(".")
                            fileExtension2 = fileExtension2.pop()

                            system.log("Processing files '" + inputFile1 + "' and '" + file + "'...")

                            // Sets the output file.
                            if (outputFile == "") {
                                outputFile = fileName2 + "-dcca.csv"
                            }

                            inputData1 = system.loadCSV(inputFile1, linesToSkip, separator, true, true)
                            inputData2 = system.loadCSV(file, linesToSkip, separator, true, true)

                            // Define the boxes sizes.
                            npts = inputData1.length
                            if (minbox < 2 * nfit) {
                                minbox = 2 * nfit
                            }
                            if (maxbox == 0 || maxbox > math.round(npts / 4)) {
                                maxbox = math.round(npts / 4)
                            }
                            if (minbox > maxbox) {
                                temp = minbox
                                minbox = maxbox
                                maxbox = temp
                                if (minbox < 2 * nfit) {
                                    minbox = 2 * nfit
                                }
                            }

                            // Create the scale matrix.
                            rs = dfa.getRScale(minbox, maxbox, math.pow(2.0, 1.0/8.0), rsmode)

                            // Calculates the cumulative sum.
                            cumulativeSum1 = dfa.getCumulativeSum(inputData1, mavg)
                            cumulativeSum2 = dfa.getCumulativeSum(inputData2, mavg)
                            
                            // Calculates the DCCA.
                            dccaData = this.getDcca(cumulativeSum1, cumulativeSum2, nfit, rs)

                            // Saves the DCCA data.
                            fileContents = system.createCSV(dccaData, separator)
                            fs.writeFileSync(outputFile, fileContents)

                            outputFile = ""
                        }
                    }
                } elseif (calculateRhoDCCA) {
                    glob = system.require("glob")

                    // Get the file 1 without extension.
                    fileName1 = inputFile1.split(".")
                    fileName1 = fileName1.shift()
                    // Get the file name extension.
                    fileExtension1 = inputFile1.split(".")
                    fileExtension1 = fileExtension1.pop()
                    
                    files = glob.sync(inputFile2)
                    
                    if (files.length == 0) {
                        system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
                    } else {
                        system.log("Processing " + files.length + " files...")

                        for (fileNumber = 0; fileNumber < files.length; fileNumber = fileNumber + 1) {
                            // Get the full file name.
                            file = files[fileNumber]
                            
                            // Get the file without extension.
                            fileName2 = file.split(".")
                            fileName2 = fileName2.shift()
                            // Get the file name extension.
                            fileExtension2 = file.split(".")
                            fileExtension2 = fileExtension2.pop()

                            system.log("Processing files '" + inputFile1 + "' and '" + file + "'...")

                            // Sets the output file.
                            if (outputFile == "") {
                                outputFile = fileName2 + "-rhodcca.csv"
                            }

                            inputData1 = system.loadCSV(inputFile1, linesToSkip, separator, true, true)
                            inputData2 = system.loadCSV(file, linesToSkip, separator, true, true)

                            // Define the boxes sizes.
                            npts = inputData1.length
                            if (minbox < 2 * nfit) {
                                minbox = 2 * nfit
                            }
                            if (maxbox == 0 || maxbox > math.round(npts / 4)) {
                                maxbox = math.round(npts / 4)
                            }
                            if (minbox > maxbox) {
                                temp = minbox
                                minbox = maxbox
                                maxbox = temp
                                if (minbox < 2 * nfit) {
                                    minbox = 2 * nfit
                                }
                            }

                            // Create the scale matrix.
                            rs = dfa.getRScale(minbox, maxbox, math.pow(2.0, 1.0/8.0), rsmode)

                            // Calculates the cumulative sum.
                            cumulativeSum1 = dfa.getCumulativeSum(inputData1, mavg)
                            cumulativeSum2 = dfa.getCumulativeSum(inputData2, mavg)
                            
                            // Set sliding window.
                            sw = 1

                            // Calculates the DFA 1.
                            dfa1 = dfa.getDfa(cumulativeSum1, nfit, rs, sw)
                            
                            // Calculates the DFA 2.
                            dfa2 = dfa.getDfa(cumulativeSum2, nfit, rs, sw)
                            
                            // Calculates the DCCA.
                            dcca = dfa.getDcca(cumulativeSum1, cumulativeSum2, nfit, rs)
                            
                            // Calculates the rhoDCCA.
                            rhoDccaData = this.getRhoDcca(dfa1, dfa2, dcca)
                            
                            // Saves the rhoDCCA data.
                            fileContents = system.createCSV(rhoDccaData, separator)
                            fs.writeFileSync(outputFile, fileContents)

                            outputFile = ""
                        }
                    }
                } else {
                    system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                    system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
                }
            } else {
                system.log(maiastatistics.appTitle + " Command Line Interface (CLI)")
                system.log("Usage: " + maiastatistics.appName + " [options] [input file] [--] [arguments]")
            }
        }
    }
}
// Run the MaiaApp code if this script has been invoked
// from the command line.
if (core.type(process) != "undefined") {
    maiastatistics.run()
}